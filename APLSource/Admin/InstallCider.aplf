 InstallCider;C;aplVersion;selectFlag;os;installFolder;ind;ZIPs;zipFilename;q;res;if1;if2
⍝ Installs Cider into the appropriate folder, depending on the current OS.
⍝ The user will be asked whether to install into the version specific folder or agnostic folder
 C←##.Cider
 aplVersion←# ⎕WG'APLVersion'
 selectFlag←0
 :Select os←C.APLTreeUtils2.GetOperatingSystem ⍬
 :Case 'Win'
     :If ~⎕NEXISTS installFolder←if1←(2⊃4070⌶0),'\SessionExtensions\CiderTatin\Cider'
     :AndIf ~⎕NEXISTS installFolder←if2←(1⊃⎕NPARTS 2⊃4070⌶0),'Dyalog APL Files\SessionExtensions\CiderTatin\Cider'
         selectFlag←1
     :EndIf
 :Case 'Lin'
     :If ~⎕NEXISTS installFolder←if1←'/home/',⎕AN,'/.dyalog/dyalog.files/SessionExtensions/CiderTatin/Cider'
     :ElseIf ~⎕NEXISTS installFolder←if2←'/home/',⎕AN,'/.dyalog/','dyalog.',('.'~⍨{⍵/⍨2>+\⍵='.'}2⊃aplVersion),'U',(¯2↑1⊃aplVersion),'.files/SessionExtensions/CiderTatin/Cider'
         selectFlag←1
     :EndIf
 :Case 'Mac'
     :If ~⎕NEXISTS installFolder←if1←'/Users/',⎕AN,'/.dyalog/dyalog.files/SessionExtensions/CiderTatin/Cider'
     :ElseIf ~⎕NEXISTS installFolder←if2←'/Users/',⎕AN,'/.dyalog/','dyalog.',('.'~⍨{⍵/⍨2>+\⍵='.'}2⊃aplVersion),'U',(¯2↑1⊃aplVersion),'.files/SessionExtensions/CiderTatin/Cider'
         selectFlag←1
     :EndIf
 :Else
     ⎕←'OS <',os,'> is not supported' ⋄ →0
 :EndSelect
 :If selectFlag
     ind←'Into which folder?'C.CommTools.Select'Version specific' 'Version agnostic'
     :If 0=≢ind
         ⎕←'Cancelled by user' ⋄ →0
     :ElseIf ind=1
         installFolder←if1
     :ElseIf ind=2
         installFolder←if2
     :Else
         ∘∘∘  ⍝ Huh?!
     :EndIf
 :EndIf
 ZIPs←C.FilesAndDirs.ListFiles ##.CiderConfig.HOME,'/Dist/*.zip'
 Assert 1=≢ZIPs
 zipFilename←⊃ZIPs
 q←'Installing:',(⎕UCS 13),('  ',zipFilename),(⎕UCS 13),'into:',(⎕UCS 13),'  ',installFolder,' ?'
 :If C.CommTools.YesOrNo q
     {}##.Cider.FilesAndDirs.RmDir installFolder
     3 ⎕MKDIR installFolder
     res←⎕SE.Tatin.InstallPackages(⊃ZIPs)installFolder
     Assert 1=≢res
     Assert(1⊃res)≡(2⊃⎕NPARTS zipFilename)
     ⎕←'*** Cider installed in ',installFolder
 :EndIf
