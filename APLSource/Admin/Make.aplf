 Make flag;pathToAPLTreeSource;targetPath;M;A;name;F;myUCMDsFolder;projectPath;codePath;toFolder;version;filename;filenames;LF
⍝ Version 2.1.0 from 2022-12-13
 :If flag
     projectPath←##.CiderConfig.HOME,'/'
     codePath←##.CiderConfig.HOME,'/',##.CiderConfig.CIDER.source,'/'
     targetPath←##.CiderConfig.HOME,'/Dist/'
     M←⎕SE.MakeHelpers
     myUCMDsFolder←M.GetMyUCMDsFolder''
     name←'Cider'
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
     {}M.IncreaseBuildID'#.',name,'.',name,'.Version'
     version←2⊃##.Cider.Version
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
     {}M.RecreateFolder targetPath
     ⎕MKDIR targetPath,'Cider'
     filenames←'Cider.aplc' 'Cider_uc.dyalog'
     LF←⎕UCS 10
     :For filename :In filenames
         :If ⎕NEXISTS myUCMDsFolder,'/Cider/',filename
         :AndIf (myUCMDsFolder,'/Cider/',filename)##.FilesAndDirs.YoungerThan ##.CiderConfig.HOME,'/APLSource/',filename
         :AndIf ##.CommTools.YesOrNo filename,' in:',LF,'  ',myUCMDsFolder,LF,'appears to be younger than in the project - copy it over?'
             (##.CiderConfig.HOME,'/APLSource/',filename)⎕NCOPY⍠('IfExists' 'Replace')⊢myUCMDsFolder,'/Cider/',filename
         :EndIf
     :EndFor
     (codePath,name,'_uc.dyalog')M.CopyTo targetPath,'Cider/'
     (codePath,name,'.aplc')M.CopyTo targetPath,'Cider/'
     (projectPath,'.',name)M.CopyTo targetPath,'Cider/'
     (projectPath,'cider.config.template')M.CopyTo targetPath,'Cider/'
     (projectPath,'html/*.html')M.CopyTo targetPath,'Cider/'
     (projectPath,'packages')M.CopyTo targetPath,'Cider/'
     {}M.CreateZipFile name targetPath version
     :If 1 M.YesOrNo'Copy the Cider user command over to',myUCMDsFolder,' ?'
         toFolder←myUCMDsFolder,'/Cider'
         {}M.RecreateFolder toFolder
         toFolder ⎕NCOPY⍠('IfExists' 'Replace')⊣targetPath,'Cider/Cider.aplc'
         toFolder ⎕NCOPY⍠('IfExists' 'Replace')⊣targetPath,'Cider/Cider_uc.dyalog'
         toFolder ⎕NCOPY⍠('IfExists' 'Replace')⊣targetPath,'Cider/cider.config.template'
         toFolder ⎕NCOPY⍠('IfExists' 'Replace')⊣targetPath,'Cider/.cider'
         {}##.FilesAndDirs.RmDirByForce toFolder,'/packages'
         toFolder ⎕NCOPY targetPath,'Cider/packages'
         (projectPath,'html/*.html')M.CopyTo toFolder
     :EndIf
     ⎕←'*** Successfully build: ',name,' version ',version
 :EndIf
