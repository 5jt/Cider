 {batch}Make flag;M;C;path;zipFilename;res;version;cfg;parms;folder1;folder2;targetFolder;developmentWas;noOf
⍝ "Make" a new version of this project.
⍝ If called by a program rather executed by a human specify a 1 as left arguments: not questions are asked, but
⍝ Cider won't be installed as a user command then.
 :If flag
     'Runs only on Windows'Assert'Win'≡##.Cider.APLTreeUtils2.GetOperatingSystem ⍬
     batch←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'batch'
     M←##.TestCases.MakeHelpers
     C←##.CiderConfig
     path←C.HOME,'/',C.CIDER.distributionFolder
     cfg←⎕JSON⍠('Dialect' 'JSON5')⊢⊃⎕NGET C.HOME,'/apl-package.json'
     targetFolder←(2⊃4070⌶0),'\SessionExtensions\CiderTatin\Cider'
     :If 0=⎕NEXISTS targetFolder,'\Cider_UC.dyalog'
         targetFolder←(⊃1 ⎕NPARTS 2⊃4070⌶0),'Dyalog APL Files\SessionExtensions\CiderTatin\Cider'
     :AndIf 0=⎕NEXISTS targetFolder,'\Cider_UC.dyalog'
     :AndIf ~1 M.##.CommTools.YesOrNo'Could not find the user command script in the user''s home folder anywhere - continue?'
         :Return
     :EndIf
     M.FetchLaterUserCommand cfg C targetFolder
     version←M.CompileVersionNumberPattern cfg
     M.RecreateFolder path
     parms←⎕SE.Tatin.CreateBuildParms C.HOME
     parms.targetPath←path
     parms.version←version
     noOf←M.ConvertMarkdownToHtml5(⊂C.HOME,'/'),¨'docs/' 'html/'
     Assert 4=noOf
     M.CreateAPI #.Cider.Cider ##.Cider.Public cfg.api
     zipFilename←⎕SE.Tatin.BuildPackage parms
     MakeZipForDyalog 1
     ⎕←'*** New version build successfully:',M.CR,'   ',zipFilename
     :If 0=batch
         ⎕←'   ',4↓0 #.Cider.Admin.InstallCider 1
     :EndIf
 :EndIf
 ⍝Done
