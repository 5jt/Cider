 Make flag;M;C;path;zipFilename;res;version;cfg;parms;folder1;folder2;targetFolder;developmentWas
⍝ "Make" a new version of this project
 :If flag
     M←##.TestCases.MakeHelpers
     C←##.CiderConfig
     :If 0<⎕SE.Cider.⎕NC'DEVELOPMENT'
         developmentWas←⎕SE.Cider.DEVELOPMENT
         ⎕SE.Cider.⎕EX'DEVELOPMENT'
     :EndIf
     path←C.HOME,'/',C.CIDER.distributionFolder
     cfg←⎕JSON⍠('Dialect' 'JSON5')⊢⊃⎕NGET C.HOME,'/apl-package.json'
     targetFolder←(2 ⎕NQ #'GetEnvironment' 'USERPROFILE'),'\Documents\Dyalog APL Files\SessionExtensions\CiderTatin\Cider'
     M.FetchLaterUserCommand cfg C targetFolder
     version←M.CompileVersionNumberPattern cfg
     M.RecreateFolder path
     parms←⎕SE.Tatin.CreateBuildParms C.HOME
     parms.targetPath←path
     parms.version←version
     M.CreateAPI #.Cider.Cider ##.Cider.Public cfg.api
     1 UpdateBuildNoInVersionFunction 1
     zipFilename←⎕SE.Tatin.BuildPackage parms
     ⎕←'*** New version build successfully:',M.CR,'   ',zipFilename
     :If 0<≢cfg.userCommandScript
         :If 1 M.YesOrNo'Install new version in ',({'%USERPROFILE%',⍵↓⍨¯2+1⍳⍨'Documents'⍷⍵}targetFolder),' ?'
             {}M.##.FilesAndDirs.RmDirByForce targetFolder
             res←⎕SE.Tatin.InstallPackages zipFilename targetFolder
             ⎕←'  New version installed: ',res
         :EndIf
     :EndIf
     :If 0<⎕NC'developmentWas'
         ⎕SE.Cider.DEVELOPMENT←developmentWas
     :EndIf
 :EndIf
 ⍝Done
