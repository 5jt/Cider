 R←Test_Open_001(stopFlag batchFlag);⎕TRAP;folder;parms;rc;success;log;res;list;msg
⍝ Open a simple project without providing any parameter but the mandatory folder
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 folder←∆GetTestPath⊃⎕SI

 parms←⎕NS''
 parms.folder←folder
 parms.quietFlag←1
 parms.watch←'ns'
 (success log)←##.Cider.Cider.OpenProject parms

 →T.GoToTidyUp~success
 →T.GoToTidyUp 1≠≢#.TEMP.⎕NL 3
 list←' '~¨⍨↓#.TEMP.⎕NL(⍳16)~3
 →T.GoToTidyUp 0≠≢list~'CiderConfig' 'TatinVars'
 →T.GoToTidyUp 9≠#.TEMP.⎕NC'CiderConfig'
 →T.GoToTidyUp 9≠#.TEMP.⎕NC'TatinVars'
 →T.GoToTidyUp≢/FilesAndDirs.EnforceSlash¨folder #.TEMP.TatinVars.HOME

 R←T._OK

∆TidyUp:
 res←∆LINK'Break' '#.TEMP'
 Assert res ∆StartsWith'Unlinked:'
 #.⎕EX'TEMP'
⍝Done
