 R←Test_UC_045(stopFlag batchFlag);⎕TRAP;res;msg;rc;folder;parms;tempDir;success;log
⍝ Exercise ]Cider.AddNuGetDependencies and ListNuGetDependencies
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If 1≠⊃1⊃2250⌶0
     R←T._NotApplicable ⋄ →0
 :EndIf

 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''
 folder←∆GetTestPath⊃⎕SI
 tempDir←∆GetFolderInTempDir⊃⎕SI
 tempDir ⎕NCOPY⍠1⊢folder,'/*'

 parms←##.Cider.CreateOpenParms''
 parms.folder←∊⎕NPARTS tempDir
 parms.noPkgLoad←0
 parms.batch←1
 parms.ignoreUserExec←1

 ##.Cider.C.YesOrNo_Answers←1 2⍴'ChangeCurrentDir@' 'n'
 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp~success
 →T.GoToTidyUp∨/'ERROR'⍷log

 {}##.Cider.AddNuGetDependencies'Clock,NodaTime'tempDir   ⍝ Clock is already installed

 (rc msg res)←∆UCMD'ListNuGetDependencies "',tempDir,'"'
 →T.PassesIf rc=0
 →T.PassesIf 0=≢msg
 →T.PassesIf 2=≢res
 →T.PassesIf 2=2⊃⍴res

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 {}∆LINK'Break' '#.TEMP'
 FilesAndDirs.RmDir tempDir,'/..'   ⍝ Does not work: the .NET DLL blocks this; there is no way around this at the time of writing (October 2023)
⍝Done
