 R←Test_NuGet_003(stopFlag batchFlag);⎕TRAP;folder;parms;res;log;tempDir
⍝ Open a project which has a Tatin dependency, add a NuGet package, close and reopen

 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''

 folder←∆GetTestPath⊃⎕SI
 tempDir←FilesAndDirs.GetTempSubDir⊃⎕SI
 tempDir ⎕NCOPY⍠1⊢ folder,'/*'

 parms←⎕NS''
 parms.folder←∊⎕NPARTS tempDir
 parms.noPkgLoad←0
 parms.batch←1

 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp ~success

 res←##.Cider.AddNuGetDependencies 'Clock' tempDir
 →T.GoToTidyUp ~(⊂'UtcNow')∊#.TEMP.dotnet.Clock.⎕NL -2

 res←##.Cider.ListDependencies tempDir 'both'
 →T.GoToTidyUp ~∧/(('nuget' 'Clock')('tatin' 'aplteam-Tester2'))∊↓res[;1 2]

 res←##.Cider.CloseProject tempDir
 →T.GoToTidyUp res≠1

 #.TEMP.(⎕EX ⎕NL -⍳9)
 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp ~(⊂'UtcNow')∊#.TEMP.dotnet.Clock.⎕NL -2
 res←##.Cider.ListDependencies tempDir 'both'
 →T.GoToTidyUp ~∧/(('nuget' 'Clock')('tatin' 'aplteam-Tester2'))∊↓res[;1 2]

res←##.Cider.CloseProject tempDir
 →T.GoToTidyUp res≠1

 R←T._OK
 →0 ⍝ If we get here, we *have* tidied up

∆TidyUp:
 res←∆LINK'Break' '#.TEMP'
 ⍝ Assert res ∆StartsWith'Unlinked:'
