 r←Test_UC_004(stopFlag batchFlag);⎕TRAP;tempDir;config;res;rc
⍝ Exercise the user command "CreateProject" with a path and a namespace name
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←FilesAndDirs.GetTempSubDir(⊃⎕SI),'_1'
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')
 #.⎕SHADOW'Foo' ⋄ 'Foo'#.⎕NS''

 config←∆GetJSON5 tempDir,'/cider.config'
 (⊂⎕JSON⍠('Compact' 0)('Dialect' 'JSON5')⊣config)⎕NPUT(tempDir,'/cider.config')1

 ⎕SE.Cider.##.C.YesOrNo_Answers←1 2⍴'TargetNotEmpty@' 'y'
 ⎕SE.Cider.##.C.YesOrNo_Answers⍪←'OpenAsWell@' 'y'
 res←⎕SE.UCMD'Cider.CreateProject "',tempDir,'" #.Foo -noEdit -acceptConfig -ignoreUserExec -batch'
 'You did not just press <enter>!'Assert∨/'Project successfully loaded and established in'⍷res
 →T.GoToTidyUp~(⊂'#.Foo')∊{⍵[;1]}##.Cider.ListOpenProjects 0

 r←T._OK

∆TidyUp:
 ⎕SE.Cider.##.C.Cleanup
 res←∆LINK'Break' '#.Foo'
 Assert res ∆StartsWith'Unlinked:'
 rc←FilesAndDirs.RmDir tempDir
 Assert 0=+/∊rc
⍝Done
