 R←Test_Misc_003(stopFlag batchFlag);⎕TRAP;res;folder;parms;list;filename;cfg;was;rc
⍝ Exercise the ListOpenProjects function with ExecuteAfterProjectOpen set
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 'TEMP'#.⎕NS''
 'TEMP2'#.⎕NS''
 #.TEMP2.∆FLAG←0

 folder←∆GetTestPath⊃⎕SI

 filename←##.Cider.GetCiderConfigFilename
 cfg←⎕JSON⍠('Dialect' 'JSON5')⊣⊃⎕NGET filename

 #.TEMP2.⎕FX'ExecuteAfterProjectOpen config' '∆FLAG←1'

 was←cfg.ExecuteAfterProjectOpen
 cfg.ExecuteAfterProjectOpen←'#.TEMP2.ExecuteAfterProjectOpen'
 (⊂⎕JSON⍠('Compact' 0)('Dialect' 'JSON5')⊣cfg)⎕NPUT filename 1

 parms←⎕NS''
 parms.folder←folder,'/Project1'
 parms.quietFlag←1
 res←##.Cider.OpenProject parms
 →T.GoToTidyUp~res
 →T.GoToTidyUp 1≠#.TEMP2.∆FLAG

 R←T._OK

∆TidyUp:
 cfg.ExecuteAfterProjectOpen←was
 (⊂⎕JSON⍠('Dialect' 'JSON5')('Compact' 0)⊣cfg)⎕NPUT filename 1
 res←∆LINK'Break' '#.TEMP'
 Assert res ∆StartsWith'Unlinked:'
 #.⎕EX¨'TEMP' 'TEMP2'
⍝Done
