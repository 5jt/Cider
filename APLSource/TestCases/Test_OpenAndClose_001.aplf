 R←Test_OpenAndClose_001(stopFlag batchFlag);⎕TRAP;tempDir;list;sourceFolder1;projectFolder1;projectFolder2;sourceFolder2;aliase;rc;res;buff;msg
⍝ Open two (new) projects and then close both of them, one by alias and one by path. Uses `DropAlias`.
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempDir←FilesAndDirs.GetTempSubDir⊃⎕SI
 projectFolder1←tempDir,'/Foo'
 sourceFolder1←projectFolder1,'/APLSource'
 'Create!'FilesAndDirs.CheckPath sourceFolder1
 (projectFolder1,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')
 #.⎕EX'Foo' ⋄ 'Foo'#.⎕NS''
 (rc msg buff)←∆UCMD'CreateProject "',projectFolder1,'" #.Foo -alias=test-case-alias-Foo -noEdit -acceptConfig -quiet'
 Assert rc=0
 #.Foo.⎕FX'r←Hello' 'r←''World'''
 {}⎕SE.Link.Add'#.Foo.Hello'
 list←FilesAndDirs.ListFiles projectFolder1
 →T.GoToTidyUp 1≠≢list
 →T.GoToTidyUp'cider.config'≢⊃,/1↓⎕NPARTS 1⊃list
 aliase←##.Cider.Cider.GetAliasFileContent
 →T.GoToTidyUp~(⊂⎕C'test-case-alias-Foo')∊aliase[;1]

 projectFolder2←tempDir,'/Goo'
 'create!'FilesAndDirs.CheckPath projectFolder2
 sourceFolder2←projectFolder2,'/APLSource'
 (projectFolder2,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Goo')
 #.⎕EX'Goo' ⋄ 'Goo'#.⎕NS''
 #.Goo.⎕FX'r←Hello' 'r←''Galaxy'''
 (rc msg buff)←∆UCMD'CreateProject "',projectFolder2,'" #.Goo -alias=test-case-alias-Goo -noEdit -acceptConfig -quiet'
 Assert rc=0
 list←FilesAndDirs.ListFiles projectFolder2
 →T.GoToTidyUp 1≠≢list
 →T.GoToTidyUp'cider.config'≢⊃,/1↓⎕NPARTS 1⊃list
 aliase←##.Cider.Cider.GetAliasFileContent
 →T.GoToTidyUp~∧/(⎕C'test-case-alias-Foo' 'test-case-alias-Goo')∊aliase[;1]

 →T.GoToTidyUp 1≠##.Cider.Cider.CloseProject'#.Foo'
 →T.GoToTidyUp 1≠##.Cider.Cider.CloseProject'[test-case-alias-Goo]'
 →T.GoToTidyUp 0≠##.Cider.Cider.CloseProject'[Does not exist]'

 R←T._OK

∆TidyUp:
 ∆LINK'Break' '#.Foo'  ⍝ We DO NOT check here because when everything is working fine...
 ∆LINK'Break' '#.Goo'  ⍝ ... then it's not linked anymore.
 rc←FilesAndDirs.RmDir tempDir
 Assert 0=+/∊rc
 #.⎕EX¨'Foo' 'Goo'
 ⎕SE.Cider.DropAlias'test-case-alias-Foo' 'test-case-alias-Goo'
⍝Done
