 R←Test_Open_082(stopFlag batchFlag);⎕TRAP;parms;r;rc;tempDir;log;success;ref
⍝ Tries to open a project that has packages but the installed packages and the build list don't fit
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempDir←∆GetFolderInTempDir⊃⎕SI
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''
 tempDir ⎕NCOPY⍠('Wildcard' 1)⊣##.CiderConfig.HOME,'/TestData/PksDontFitBuildList/*'

 parms←##.Cider.CreateOpenParms ⍬
 parms.folder←tempDir
 parms.parent←'#.TEMP'
 parms.watch←'ns'
 parms.ignoreUserExec←1
 parms.batch←1
 ref←∆GetRefToCommTools
 ref.YesOrNo_Answers←1 2⍴'ReinstallBecauseOfInconsistency@' 'y'
 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp∨/'ERROR'⍷log
 →T.GoToTidyUp'#._tatin.aplteam_Tester2_3_5_0.Tester2'≢⍕#.TEMP.fooproj.Tester2  ⍝ VALUE ERROR? Try again, but first execute stuff after ∆TidyUp
 →T.GoToTidyUp'#._tatin.aplteam_CodeCoverage_0_7_3.CodeCoverage'≢⍕#.TEMP.fooproj.CodeCoverage

 R←T._OK

∆TidyUp:
 ref.Cleanup
 {}⎕SE.Link.Break'#.TEMP.fooproj'
 {}FilesAndDirs.RmDirByForce tempDir
⍝Done
