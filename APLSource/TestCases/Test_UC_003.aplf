 r←Test_UC_003(stopFlag batchFlag);⎕TRAP;tempDir;config;res;rc;ref
⍝ Exercise the user command "CreateProject" with just a path but without the flag
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←∆GetFolderInTempDir(⊃⎕SI),'_1'
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')

 config←∆GetJSON5 tempDir,'/cider.config'
 (⊂⎕JSON⍠('Compact' 0)('Dialect' 'JSON5')⊣config)⎕NPUT(tempDir,'/cider.config')1

 ref←∆GetRefToCommTools
 ref.YesOrNo_Answers←1 2⍴'Project successfully created; open as well?' 'y'
 res←⎕SE.UCMD'Cider.CreateProject "',tempDir,'" -noEdit -acceptConfig -ignoreUserExec -batch'

 →T.GoToTidyUp~(⊂'#.Foo')∊{⍵[;1]}##.Cider.ListOpenProjects 0

 r←T._OK

∆TidyUp:
 ref.Cleanup
 res←∆LINK'Break' '#.Foo'
 #.⎕EX'Foo'
 Assert res ∆StartsWith'Unlinked:'
 rc←FilesAndDirs.RmDir tempDir
 Assert 0=+/∊rc
⍝Done
