 R←Test_OpenAndClose_003(stopFlag batchFlag);⎕TRAP;tempDir;list;res;parms
⍝ Open a (new) project and then attempt to close a non-existing one (was once a bug)
⍝ The bug: closed ALL projects rather than none
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')

 R←T._Failed
 tempDir←FilesAndDirs.GetTempSubDir⊃⎕SI
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')

 {}⎕SE.UCMD'Cider.CreateProject "',tempDir,'" -noEdit -acceptConfig'

 parms←##.Cider.CreateOpenParms ⍬
 parms.folder←tempDir
 parms.quietFlag←1
 res←##.Cider.OpenProject parms

 list←##.Cider.ListOpenProjects 0
 →T.GoToTidyUp 0≠##.Cider.CloseProject'DoesNotExist'
 →T.GoToTidyUp list≢##.Cider.ListOpenProjects 0
 →T.GoToTidyUp 1≠##.Cider.CloseProject'#.Foo'
 →T.GoToTidyUp(≢list)≢1+≢##.Cider.ListOpenProjects 0

 R←T._OK

∆TidyUp:
 FilesAndDirs.RmDir tempDir
 #.⎕EX'Foo'
⍝Done
