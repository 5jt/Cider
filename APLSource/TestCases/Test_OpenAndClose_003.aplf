 R←Test_OpenAndClose_003(stopFlag batchFlag);⎕TRAP;tempDir;list;res;rc
⍝ Open a (new) project and then attempt to close a non-existing one (was once a bug)
⍝ The bug: closed ALL projects rather than none
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempDir←FilesAndDirs.GetTempSubDir⊃⎕SI
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')

 {}⎕SE.UCMD'Cider.CreateProject "',tempDir,'" -noEdit -acceptConfig -quiet'

 list←##.Cider.Cider.ListOpenProjects 0
 →T.GoToTidyUp 0≠##.Cider.Cider.CloseProject'DoesNotExist'
 →T.GoToTidyUp list≢##.Cider.Cider.ListOpenProjects 0
 →T.GoToTidyUp 1≠##.Cider.Cider.CloseProject'#.Foo'
 →T.GoToTidyUp(≢list)≢1+≢##.Cider.Cider.ListOpenProjects 0

 R←T._OK

∆TidyUp:
 ∆LINK'Break' '#.Foo'  ⍝ We DO NOT check here because when everything is working fine then it's not linked anymore.
 rc←FilesAndDirs.RmDir tempDir
 Assert 0=+/∊rc
 #.⎕EX'Foo'
⍝Done
