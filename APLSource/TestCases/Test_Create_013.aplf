 R←Test_Create_013(stopFlag batchFlag);⎕TRAP;tempDir;list;res;stop;msg;rc;ref
⍝ Attempts to create and then open a project with a non-empty namespace and a non-empty folder
⍝ A config file is copied over in order to avoid user interaction
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempDir←∆GetFolderInTempDir⊃⎕SI
 3 ⎕MKDIR tempDir,'/APLSource'
 'Foo'#.⎕NS''
 #.Foo.⎕FX'r←TheAnswerIs' 'r←43'
 (⊂'r←TheAnswerIs' 'r←42')⎕NPUT tempDir,'/APLSource/TheAnswerIs.aplf'
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')

 ref←∆GetRefToCommTools
 ref.YesOrNo_Answers←2 2⍴''
 ref.YesOrNo_Answers[1;]←'OpenAsWell@' 'y'
 ref.YesOrNo_Answers[2;]←'TargetNotEmpty@' 'Y'

 stop←0
 (rc msg res)←stop ∆UCMD'CreateProject "',tempDir,'" -acceptConfig -noEdit -ignoreUserExec'   ⍝ We cannot specify -batch here!
 →T.GoToTidyUp 0<rc
 →T.GoToTidyUp #.Foo.TheAnswerIs≠42  ⍝ Original (43) overwritten by the version in the source folder

 R←T._OK

∆TidyUp:
 {}⎕SE.Link.Break'#.Foo'
 rc←⊃FilesAndDirs.RmDir tempDir
 Assert rc∧.=0
 #.⎕EX'Foo'
 ref.Cleanup
⍝Done
