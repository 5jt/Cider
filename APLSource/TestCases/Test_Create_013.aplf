 R←Test_Create_013(stopFlag batchFlag);⎕TRAP;tempDir;list;res;stop;msg;rc
⍝ Attempts to create and then open a project with a non-empty namespace and a non-empty folder
⍝ A config file is copied over in order to avoid user interaction
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempDir←FilesAndDirs.GetTempSubDir⊃⎕SI
 3 ⎕MKDIR tempDir,'/APLSource'
 'Foo'#.⎕NS''
 #.Foo.⎕FX'r←TheAnswerIs' 'r←43'
 (⊂'r←TheAnswerIs' 'r←42')⎕NPUT tempDir,'/APLSource/TheAnswerIs.aplf'
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')

 ⎕SE.Cider.##.CommTools.YesOrNo_Answers←2 2⍴''
 ⎕SE.Cider.##.CommTools.YesOrNo_Answers[1;]←'Project successfully created; open as well?' 'y'
 ⎕SE.Cider.##.CommTools.YesOrNo_Answers[2;]←'Target namespace "#.Foo" is not empty. Delete contents?' 'Y'

 stop←0
 (rc msg res)←stop ∆UCMD'CreateProject "',tempDir,'" -acceptConfig -noEdit'

 →T.GoToTidyUp 0<rc
 →T.GoToTidyUp #.Foo.TheAnswerIs≠42  ⍝ Original (43) overwritten by the version in the source folder

 R←T._OK

∆TidyUp:
 {}⎕SE.Link.Break'#.Foo'
 rc←⊃FilesAndDirs.RmDir tempDir
 Assert rc∧.=0
 #.⎕EX'Foo'
 ⎕SE.Cider.##.CommTools.Cleanup
⍝Done
