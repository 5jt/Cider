 R←Test_UC_051(stopFlag batchFlag);⎕TRAP;folder;res;log;cals;success;tempDir;config;msg;rc;stop;batch;ref
⍝ Open project, add 2 NuGet packages to non-default folder, load, call, list, close, re-open, close
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If 1≠⊃1⊃2250⌶0
     R←T._NotApplicable ⋄ →0
 :EndIf

 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''
 folder←∆GetTestPath⊃⎕SI
 tempDir←∆GetFolderInTempDir⊃⎕SI
 tempDir ⎕NCOPY⍠1⊢folder,'/*'

 ref←∆GetRefToCommTools
 ref.YesOrNo_Answers←1 2⍴'ChangeCurrentDir@' 'n'
 stop←0
 (rc msg res)←stop ∆UCMD'OpenProject ',tempDir,' -projectSpace=TEMP -batch -ignoreUserExec'
 Assert rc=0

 ref.YesOrNo_Answers←1 2⍴'LoadNuGetDependenciesAfterAdding@' 'y'
 (rc msg res)←stop ∆UCMD'AddNuGetDependencies clock,NODATIME ',tempDir
 Assert rc=0
 →T.GoToTidyUp~'Clock' 'NodaTime'∆LazyMatch res

 (rc msg res)←stop ∆UCMD'ListNuGetDependencies ',tempDir
 Assert rc=0
 →T.GoToTidyUp 2≠≡res

 (rc msg res)←∆UCMD'CloseProject ',tempDir
 Assert rc=0
 →T.GoToTidyUp'Number of projects closed: 1'≢res

 #.TEMP.(⎕EX ⎕NL-⍳9)
 (rc msg res)←stop ∆UCMD'OpenProject ',tempDir,' -projectSpace=TEMP -batch -ignoreUserExec'
 Assert rc=0
 →T.GoToTidyUp'Project successfully loaded and established in "#.TEMP"'≢res~⎕TC
 →T.GoToTidyUp~(⊂'UtcNow')∊#.TEMP.Clock.⎕NL-2
 →T.GoToTidyUp~∧/'Julian' 'Gregorian'∊⍕¨#.TEMP.NodaTime.CalendarSystem.⎕NL-2

 (rc msg res)←∆UCMD'CloseProject ',tempDir
 Assert rc=0

 R←T._OK

∆TidyUp:
 ref.Cleanup
 res←∆LINK'Break' '#.TEMP'
 FilesAndDirs.RmDir tempDir,'/..'   ⍝ Does not work: the .NET DLL blocks this; there is no way around this at the time of writing (October 2023)
⍝Done
