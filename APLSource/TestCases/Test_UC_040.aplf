 R←Test_UC_040(stopFlag batchFlag);⎕TRAP;tempDir;sourceFolder;list;config;res;rc
⍝ Create a project from a folder without Linking a namespace; project space name defaults
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempDir←∆GetFolderInTempDir⊃⎕SI
 sourceFolder←tempDir,'/APLSource'
 'Create!'FilesAndDirs.CheckPath sourceFolder
 (⊂'r←Hello' 'r←''World''')FilesAndDirs.NPUT sourceFolder,'/hello.aplf'

 res←⎕SE.UCMD'Cider.CreateProject "',tempDir,'" -noEdit -ignoreUserExec -batch'

 list←FilesAndDirs.ListFiles tempDir
 →T.GoToTidyUp 1≠≢list
 →T.GoToTidyUp'cider.config'≢⊃,/1↓⎕NPARTS 1⊃list
 config←##.Cider.ReadProjectConfigFile tempDir
 →T.GoToTidyUp'config.CIDER.projectSpace'≡config.CIDER.projectSpace

 R←T._OK

∆TidyUp:
 res←∆LINK'Break'(#.⊃{⍺,'.',⍵}/config.CIDER.(parent projectSpace))
 Assert res ∆StartsWith'Unlinked:'
 rc←FilesAndDirs.RmDir tempDir
 Assert 0=+/∊rc
⍝Done
