 folder←{globalAliasFilename}GetFolderFromAlias2 alias;data
⍝ Translates an alias (if any) into a folder.\\
⍝ In case no global alias file can be found an empty vector is returned.\\
⍝ If the alias cannot be found an error is thrown.\\
⍝ `globalAliasFilename` should only be specified by test cases
 folder←''
 globalAliasFilename←{0<⎕NC ⍵:⍎⍵ ⋄ GetCiderAliasFilename}'globalAliasFilename'
 :If F.Exists globalAliasFilename
     :If 0<≢data←⊃F.NGET globalAliasFilename 1
         data←↑{'='(≠⊆⊢)⍵}¨data
         alias←⎕C alias~'[]'
         :If (⊂alias)∊data[;1]
             folder←2⊃data[data[;1]⍳⊂alias;]
         :Else
             ('"',alias,'" is not a valid alias')Assert(⎕NS'').{0=⎕NC ⍵}⎕C alias~'[]'
         :EndIf
     :EndIf
 :EndIf
