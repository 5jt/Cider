 r←AddNuGetDependencies (packages path);NuGet;ns;config;nuget_dir;configfile
⍝ Either add one or more NuGet packages

 :If 1=≡packages
     packages←{1↓¨(⍵∊',;')⊂⍵}';',packages
 :EndIf
 path←FilesAndDirs.EnforceSlash path

 'Unable to locate NuGet API' ⎕SIGNAL (0≡NuGet←FindNuGetAPI ⍬)/11
 ns←path{3::0 ⋄ ⊃⍵[⍵[;2]⍳⊂⍺;1]}ListOpenProjects 0 ⍝ namespace project loaded into
 config←Get_JSON5 configfile←path,'/cider.config'

 :If 0=⎕NC 'config.CIDER.dependencies.nuget'
     config.CIDER.dependencies.nuget←'nuget-packages'
     config Put_JSON5 configfile
 :EndIf

 nuget_dir←path,'/',{0::⍵ ⋄ config.CIDER.dependencies.nuget}'nuget-packages'

 r←NuGet.Setup nuget_dir
 →(∨/'error:'⍷⎕C r)⍴0

 r←NuGet.Add (⊂nuget_dir),packages
 →(∨/'error:'⍷⎕C r)⍴0

 :If ns≢0 ⍝ We have a project namespace
     :If 2=⎕NC 'ns'
         ns←⍎ns
     :EndIf
     'dotnet'ns.⎕NS''
     ns.dotnet.⎕USING←NuGet.Using nuget_dir
     r←'NuGet dependencies established in ',(⍕ns),'.dotnet'
 :EndIf
