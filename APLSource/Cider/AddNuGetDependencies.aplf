 list←AddNuGetDependencies(packages projectFolder);NuGet;msg;cfg;nuGetFolder;package;buff;targetNamespace;b
⍝ Add one or more NuGet packages to the NuGet-folder defined in the config file of `projectFolder`.
⍝ * `packages` must be either a nested vector with package names or a comma-separated list of package names
⍝ * `projectFolder` must carry a Cider config files used to establish the NuGet folder.\\
⍝ Returns a list with the names that got installed or at least updated.
 :If 1=≡packages
     packages←','(≠⊆⊢)packages
 :EndIf
 :If 0∊b←(⎕NS''){⊃0=⍺.⎕NC ⍵}¨packages
     ('Invalid package name(s): ',⊃{⍺',',⍵}/'"',¨'"',⍨¨(~b)/packages)Assert 0
 :EndIf
 :If IsAlias projectFolder
     projectFolder←GetFolderFromAlias2 projectFolder
 :EndIf
 'No Cider config file found'Assert F.Exists projectFolder,'/cider.config'
 cfg←ReadProjectConfigFile projectFolder
 nuGetFolder←projectFolder,'/',⊃GetNuGetDependencyFolders cfg
 targetNamespace←''
 :If '='∊buff←2⊃⎕NPARTS nuGetFolder
     targetNamespace←{⍵↓⍨⍵⍳'='}buff
     nuGetFolder←(-1+≢targetNamespace)↓nuGetFolder
 :EndIf
 :If 0<≢nuGetFolder
     nuGetFolder←FilesAndDirs.EnforceSlash nuGetFolder  ⍝ .NET requirement
     {}⎕MKDIR⍣(~⎕NEXISTS nuGetFolder)⊣nuGetFolder
     'Unable to locate NuGet API'Assert 0<≢NuGet←FindNuGetAPI ⍬
     msg←NuGet.Setup nuGetFolder
     msg Assert~∨/'error:'⍷⎕C msg
     list←''
     :For package :In packages
         :Trap 99
             msg←NuGet.Add(⊂nuGetFolder),⊂package
             :If ~∨/'error:'⍷⎕C msg
                 list,←⊂package
             :EndIf
         :EndTrap
     :EndFor
 :EndIf
⍝Done
