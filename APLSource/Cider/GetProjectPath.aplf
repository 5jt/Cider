 path←GetProjectPath project;list;index;aliasDefs;bool;alias;info
 path←''
 :If 0≡project
 :OrIf 0=≢project
     list←ListOpenProjects 0
     :Select ≢list
     :Case 0
         ⎕←'No open Cider project found'
         :Return
     :Case 1
         path←⊃list[1;2]
     :Else
         :If 0=≢index←'Select a Cider project:'C.Select↓⎕FMT list
             :Return
         :EndIf
         path←2⊃list[index;]
     :EndSelect
 :Else
     aliasDefs←P.GetAliasFileContent ⍬
     path←Args._1
     :If path≡'[?]'
         :If 0=≢path←SelectFromAliases aliasDefs
             :Return
         :EndIf
     :ElseIf '['=1⍴path
     :AndIf '*'=¯1↑path~'[]'
         bool←(¯1↓path~'[]'){(⎕C(≢⍺)↑[2]⍵)∧.=⎕C ⍺}↑aliasDefs[;1]
         :Select +/bool
         :Case 0
             :Return
         :Case 1
             (alias path)←aliasDefs[bool⍳1;]
             :If 0=1 C.YesOrNo'Sure you want to open "',path,'" ?'
                 :Return
             :EndIf
         :Else
             info←'(',((⍕+/bool),' of ',(⍕≢aliasDefs)),')'
             :If 0=≢path←info SelectFromAliases bool⌿aliasDefs
                 :Return
             :EndIf
         :EndSelect
     :EndIf
 :EndIf
