 {r}←CheckForDropboxConflicts path;dmx;list;msg;found
⍝ Checks whether there are any Dropbox files indicating conflictes, and if so print them to the session.
⍝ Returns 0 in case there are not any such conflict files, otherwise the number of files found.
 r←found←0
 msg←''
 :If 0<≢path~' '
 :AndIf GetGlobalDropboxConflicts
     msg,←⊂'Checking for Dropbox conflicts: ',path,' ...'
     r←≢list←'recursive'F.ListFiles path,'/*conflicted copy*'
     :If 0<r
         found←1
         msg,←⊂'  ********** The project has conflicting files! ************'
         msg,←⊂'  *** PATH: ',path
         msg,←'  '∘,¨(1+≢path)↓¨list
         msg,←⊂'  **********************************************************'
     :Else
         msg,←⊂'  No Dropbox conflicts found'
     :EndIf
 :EndIf
 :If found
     ⎕←↑msg
 :ElseIf 0<≢msg
     :If GetGlobalVerbose
         ⎕←↑msg
     :EndIf
 :EndIf
