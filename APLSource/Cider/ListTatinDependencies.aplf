 r←ListTatinDependencies folder;cfgFilename;cfg;buildFilenames;buildLists;b;tatinFolders;buildFolders;list;folder2;buildFilename
⍝ `y` must point to a project, either as alias ([]) or as a project folder.
⍝ The actual Tatin dependency folders are established from the project config file.
 r←0 5⍴''
 :If IsAlias folder
     folder←GetFolderFromAlias2 folder
 :EndIf
 cfgFilename←folder,'/cider.config'
 :If F.Exists cfgFilename
     cfg←ReadProjectConfigFile cfgFilename
     tatinFolders←GetTatinDependencyFolders cfg
     :If 0<≢tatinFolders
         tatinFolders←RemoveTargetDefinition¨tatinFolders
         buildFolders←(⊂folder,'/'),¨tatinFolders
         buildFilenames←buildFolders,¨⊂'/apl-buildlist.json'
         r⍪←tatinFolders ListTatinDependencies_ buildFilenames
     :EndIf
 :Else
     buildFilename←folder,'/apl-buildlist.json'
     :If ⎕NEXISTS buildFilename
         r⍪←(⊂{¯1↓1⊃⎕NPARTS ⍵}buildFilename)ListTatinDependencies_⊂buildFilename
     :Else
         list←F.ListDirs folder
         :For folder2 :In list
             r⍪←ListTatinDependencies folder2
         :EndFor
         :If 1={+/∧\⍵≡¨⍵[1]}⎕XSI
             r[;1]←(1+≢folder)↓¨r[;1]
             r[⍸{~(⍵⍳⍵)=⍳≢⍵}r[;1];1]←⊂''
         :EndIf
     :EndIf
 :EndIf
