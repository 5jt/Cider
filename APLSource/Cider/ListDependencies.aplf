 r←ListDependencies(path type);tatin;p
⍝ List dependencies for project at path
⍝ type: nuget|tatin|both (empty=both)

 r←0 3⍴⊂''
 :If (⊂⎕C type)∊'nuget' 'both' ''
     r⍪←(⊂'nuget'),↑ListNuGetPackages path
 :EndIf

 :If (⊂⎕C type)∊'tatin' 'both' ''
     :Trap 11 ⍝ ListTatinPackages fails if there are none
         :If 0≠≢tatin←{2=⍴⍴⍵:(3↓⍵[;2])/3↓⍵[;1] ⋄ ⍬}ListTatinPackages path
             tatin←(' '=⊃¨tatin)↓¨tatin ⍝ leading space?
             p←(⌽¨tatin)⍳¨'-'
             r⍪←(⊂'tatin'),((-p)↓¨tatin),(1-p)↑¨tatin
         :EndIf
     :EndTrap
 :EndIf
