 r←RunMake projectFolder;config
 r←''
 :If IsAlias projectFolder
     projectFolder←GetFolderFromAlias projectFolder
 :EndIf
 ('No file "cider.config" found in ',projectFolder)Assert F.Exists projectFolder,'/cider.config'
 config←ReadProjectConfigFile projectFolder
 :If 0<config.CIDER.⎕NC'make'
 :AndIf 0<≢config.CIDER.make
     :If (1⍴config.CIDER.make~' ')∊'])'
         r←config.CIDER.make,' ⍝ Execute this for creating a new version'
     :ElseIf (⊃config.CIDER.make)∊'#⎕'   ⍝ Was possible until 0.28.1
         r←config.CIDER.make,' ⍝ Execute this for creating a new version'
     :Else
         r←(config.CIDER.parent),({0=≢⍵:⍵ ⋄ '.',⍵},config.CIDER.projectSpace),'.',config.CIDER.make,' ⍝ Execute this for creating a new version'
     :EndIf
 :Else
     1 PrintToSession'No expression found for creating a new version of <',config.CIDER.projectSpace,'>'
 :EndIf
